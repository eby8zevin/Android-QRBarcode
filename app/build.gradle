plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

def BUILD_DATE = getBuildDate()

static def getBuildDate() {
    def date = new Date()
    def formattedDate = date.format("yyyy-MM-dd' 'HH:mm:ss' '")
    return formattedDate
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
} else {
    localProperties.setProperty("KEYSTORE_FILE", "keystore-qrbarcode.jks")
    localProperties.setProperty("KEYSTORE_PASSWORD", System.getenv("KEYSTORE_PASSWORD"))
    localProperties.setProperty("KEY_ALIAS", System.getenv("KEY_ALIAS"))
    localProperties.setProperty("KEY_PASSWORD", System.getenv("KEY_PASSWORD"))
}

android {
    signingConfigs {
        release {
            storeFile file(localProperties.getProperty("KEYSTORE_FILE"))
            storePassword localProperties.getProperty("KEYSTORE_PASSWORD")
            keyAlias localProperties.getProperty("KEY_ALIAS")
            keyPassword localProperties.getProperty("KEY_PASSWORD")
        }
    }

    namespace 'com.ahmadabuhasan.qrbarcode'
    compileSdkVersion 35

    defaultConfig {
        applicationId "com.ahmadabuhasan.qrbarcode"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 5
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix " build:" + BUILD_DATE
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.gms:play-services-ads:24.5.0'
    implementation platform('com.google.firebase:firebase-bom:34.0.0')
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
}